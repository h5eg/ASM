

---------------------
------ 逻辑扇区 ------
---------------------

硬盘读写的基本单位是扇区，不能只读取一个扇区的几个字节。


逻辑扇区，将硬盘上所有可用的扇区都从 0 开始编号，不用去理会磁头号，柱面号和扇区号。


----- LBA28编址方式 -----

用28比特来表示逻辑扇区号，从 0x000 0000 ~ 0xFFF FFFF，可以表示2^28次方个扇区。

每个扇区有512字节，所以 LBA28 可以管理128GB的硬盘


----- LBA48编址方式 -----

用48比特来表示逻辑扇区号，可以管理 131072TB 的硬盘容量。

1GB == 1024MB
1TB == 1024GB




------------------------
------ 硬盘端口号 -------
------------------------

ICH芯片内部一般都集成了两个 PATA接口/SATA接口 (有的甚至更多)，分别是 主硬盘接口 和 副硬盘接口。

主硬盘分配的端口号是：0x1f0 ~ 0x1f7

副硬盘分配的端口号是：0x170 ~ 0x177


0x1f0: 数据传输端口，16位大小，可以连续从该端口写入 或 读取数据

0x1f1: 错误信息端口，包含硬盘驱动器最后一次执行命令后的状态

0x1f2: 指明读写的扇区数，8位大小

       如果写入的是0值，表示读取256个扇区。

       每读取一个扇区，这个端口寄存器中保存的数值就会减一。

       如果读过程中发生错误，该端口寄存器中的数值，就代表未读取的扇区数。


0x1f3, 0x1f4, 0x1f5, 0x1f6: 都是8位大小的端口，共同指明 28位 的起始扇区号。

                            28位的扇区号太长，需要4个8位寄存器来存储。

                            硬盘控制器会从该扇区开始读取数据。

      扇区号是28位的，4个8位寄存器加起来一共是32位，端口 0x1f6 中的后四位有专门的用途。

      第0位～第3位: 用来表达扇区号最后四位值。

      第4位: 只要要操作的硬盘。0值代表主盘，1值代表从盘。

      第5位～第7位: 全是1值，代表使用 LBA 模式。


0x1f7: 命令端口，也是状态端口，8位大小。

       读数据：将端口值设置成 0x20

       写数据：将端口值设置成 0x30

       给该端口发出命令后，硬盘就开始准备了，并将该端口的最高位设置成1 （读：1010 0000）

       当硬盘准备好后，会将最高位设置成0，同时将第3位(起始是第0位)设置成1 （0010 1000）

       一旦磁盘系统准备就绪，就可以从 数据端口0x1f0 进行读写数据了。








---------------------
------ 具体实现 ------
---------------------


1.设置要读取的扇区数量

将数值写入编号为 0x1f2 的端口中，这是一个8位大小端口，因此每次最多读写255个扇区

mov dx,0x1f2
mov al,0x01
out dx,al     ; out指令表示要将数据写入到端口中，dx指明要写入的目标端口，寄存器al中则保存了要写入的数值

; 上面三行指令，指明读取一个扇区

如果往端口中写入的数值为0，代表要读取256个扇区数。

每读取一个扇区，编号为 0x1f2的端口 所存放的值就会减一。

如果读取过程中出错，该端口中的值就代表尚未读取的扇区数。



2.设置起始的扇区号

扇区的读写是连续的，只要给出起始扇区号就行了。


LBA28编址方式，使用的是28位数值来给扇区编号。

28位的扇区号太长，需要分成4段，分别写入8位大小的端口 0x1f3, 0x1f4, 0x1f5, 0x1f6

0x1f3号端口存放0～7位，0x1f4号端口存放8～15位，0x1f5存放16～23位，最后4位存放到0x1f6号端口


例如：读写的起始扇区编号为0x02

mov dx,0x1f3  ; 设置要写入的目标端口
mov al,0x02   ; 要写入的值
out dx,al     ; 将al中的值，写入到dx指向的端口中

inc dx
mov al,0x00
out dx,al     ; 给编号为0x1f4的端口，写入0x00

inc dx
out dx,al     ; 给编号为0x1f5的端口，写入0x00

inc dx
mov al,0xe0
out dx,al     ; 将数值0xe0，写入编号为0x1f5的端口


最后一个端口，为何写入的是0xe0？

每个PATA/SATA接口允许挂载两块硬盘，分别是主盘和副盘。

编号为0x1f6的端口:

第0位~第3位: 存放逻辑扇区号的24～27位的值。

第4位: 指示操作的硬盘号，0代表操作主盘，1代表操作副盘。

高三位: 全是1表示LBA模式，全是0表示CHS模式

将高四位的值设置成 1110，刚好代表操作主盘，而 1110 刚好是十六进制的 e



3.向端口号为0x1f7的端口写入0x20，请求读硬盘

mov dx,0x1f7
mov al,0x20   ; 0010 0000
out dx,al



4.等待硬盘准备就绪

端口0x1f7，即是命令端口，也是状态端口。

给该端口发出命令后，硬盘就开始忙了，并将0x1f7端口的最高位设置成1

当硬盘忙碌结束后，再将最高位设置成0，同时将第3位(起始是第0位)设置成1 （0000 1000）

此时，硬盘的状态是，请求主机发送或是接收数据。


mov dx,0x1f7

waiting:

	in  al,dx    ; 将0x1f7编号端口中的值，复制一份传送到al中

    and al,0x88  ; 和0x88进行并且操作，al中除了第三位和最高位的其余位，一定会被清零
                 ; 0x88 == 1000 1000
                 ; (并且运算：只有同位上的值都为1，结果才是1，否则结果都是0)
                 ; 将al与0x88进行并且运算，是将除了最高位和第三位之外的其余位，都清零

	cmp al,0x08  ; 只要硬盘忙碌完毕，0x1f7端口中的值与0x88进行并且操作后，值一定是0x08

	jne waiting  ; 循环条件是不等于就成立，只要al等于0x08，循环条件就结束



5.连续取出数据

端口号为0x1f0的端口，是硬盘的数据端口，还是一个16位大小的端口。

一旦硬盘控制器空闲，且准备就绪，就可以从这个端口写入或读取数据



假设从硬盘读取一个扇区(512字节)，并将读取到的数据存放到由段寄存器DS指向的数据段，偏移地址由BX指定

mov cx,256    ; 一次读取16位，也就是两字节，512字节要读取256次

mov dx,0x1f0  ; 指明读取0x1f0编号端口的数据

readData:

	in  ax,dx     ; 读取指定端口中的数据

	mov [bx],ax   ; 将读取到的数据写入到指定内存位置

	add bx,2

	loop readData ; 还要cx的值不为0，循环就继续








